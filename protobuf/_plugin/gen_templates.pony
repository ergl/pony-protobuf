use "templates"

class val GenTemplate
  let header: Template

  // Enums
  let enum_field: Template
  let enum_alias: Template
  let enum_builder: Template

  // Message
  let message_structure: Template

  // is_initialized
  let initialized_primitive_clause: Template
  let initialized_message_clause: Template
  let initialized_required_message_clause: Template
  let initialized_repeated_clause: Template

  // compute_size
  let size_optional_clause: Template
  let size_repeated_clause: Template
  let size_packed_clause: Template

  // parse_from_stream
  let read_bytes: Template
  let read_string: Template
  let read_enum: Template
  let read_varint: Template
  let read_fixed: Template
  let read_packed_varint: Template
  let read_packed_fixed: Template
  let read_packed_enum: Template
  let read_inner_message: Template
  let read_repeated_inner_message: Template
  let read_repeated_bytes: Template
  let read_repeated_string: Template
  let read_repeated_enum: Template
  let read_repeated_varint: Template
  let read_repeated_fixed: Template

  // write_to_stream
  let write_bytes: Template
  let write_enum: Template
  let write_varint: Template
  let write_varint_zigzag: Template
  let write_fixed_32: Template
  let write_fixed_64: Template
  let write_inner_message: Template
  let write_repeated_non_message_type: Template
  let write_repeated_inner_message_clause: Template
  let write_packed_varint: Template
  let write_packed_varint_bool: Template
  let write_optional_clause: Template

  new val create() ? =>
    header = Template.parse(
      """
      // This file was autogenerated by pony-protobuf {{vsn}}. Do not edit!
      // Compiled by protoc {{protoc_version}}

      use "protobuf"

      """
    )?

    enum_field = Template.parse(
      """
      primitive {{name}} is ProtoEnumValue
        fun as_i32(): I32 => {{value}}

      """
    )?

    enum_alias = Template.parse(
      """
      type {{name}} is (
        {{first_alias}}{{ifnotempty rest_alias}}{{for x in rest_alias}}
        | {{x}}{{end}}{{end}}
      )

      """
    )?

    enum_builder = Template.parse(
      """
      primitive {{name}} is ProtoEnum
        fun from_i32(value: I32): ({{enum_type}} | None) =>
          match value{{for x in match_clauses }}
          | {{x.value}} => {{x.name}}{{end}}
          else
            None
          end

      """
    )?

    message_structure = Template.parse(
      """
      class {{name}} is ProtoMessage{{ifnotempty fields}}{{for field in fields}}
        var {{field.name}}: {{field.pony_type}} = {{field.default}}{{end}}{{end}}
        {{ifnotempty field_size_clauses}}
        fun compute_size(): U32 =>
          var size: U32 = 0{{for clause in field_size_clauses}}
          {{clause}}{{end}}
          size{{end}}
        {{ifnotempty read_clauses}}
        fun ref parse_from_stream(reader: ProtoReader) ? =>
          while reader.size() > 0 do
            match reader.read_field_tag()?{{for clause in read_clauses}}
            {{clause}}{{end}}
            | (_, let typ: TagKind) => reader.skip_field(typ)?
            end
          end{{end}}
        {{ifnotempty write_clauses}}
        fun write_to_stream(writer: ProtoWriter) =>{{for clause in write_clauses}}
          {{clause}}{{end}}{{end}}
        {{ifnotempty initializer_clauses}}
        fun is_initialized(): Bool =>{{for clause in initializer_clauses}}
          {{clause}}{{end}}
          true{{end}}

      """
    )?

    initialized_primitive_clause = Template.parse(
      """
      if {{name}} is None then
            return false
          end"""
    )?

    initialized_message_clause = Template.parse(
      """
      match {{name}}
          | None => None
          | let {{name}}': this->{{type}} =>
            if not ({{name}}'.is_initialized()) then
              return false
            end
          end"""
    )?

    initialized_required_message_clause = Template.parse(
      """
      match {{name}}
          | None => return false
          | let {{name}}': this->{{type}} =>
            if not ({{name}}'.is_initialized()) then
              return falsse
            end
          end"""
    )?

    initialized_repeated_clause = Template.parse(
      """
      for v in {{name}}.values() do
            if not v.is_initialized() then
              return false
            end
          end"""
    )?

    size_optional_clause = Template.parse(
      """
      match {{name}}
          | None => None
          | let {{name}}': {{if needs_viewpoint}}this->{{end}}{{type}} =>
            size = size + FieldSize.{{method}}{{if method_type}}[{{method_type}}]{{end}}({{number}}{{if needs_name_arg}}, {{name}}'{{end}})
          end"""
    )?

    size_repeated_clause = Template.parse(
      """
      for v in {{name}}.values() do
            size = size + FieldSize.{{method}}{{if method_type}}[{{method_type}}]{{end}}({{number}}{{if needs_name_arg}}, v{{end}})
          end"""
    )?

    size_packed_clause = Template.parse(
      """
      size = size + FieldSize.{{method}}{{if method_type}}[{{method_type}}]{{end}}({{number}}, {{name}})
      """
    )?

    read_bytes = Template.parse(
      """
      | ({{number}}, {{wire_type}}) =>
              {{name}} = reader.read_bytes()?"""
    )?

    read_string = Template.parse(
      """
      | ({{number}}, {{wire_type}}) =>
              {{name}} = reader.read_string()?"""
    )?

    read_enum = Template.parse(
      """
      | ({{number}}, {{wire_type}}) =>
              {{name}} = {{enum_builder}}.from_i32(reader.read_varint_32()?.i32())"""
    )?

    read_varint = Template.parse(
      """
      | ({{number}}, {{wire_type}}) =>
              {{name}} = reader.read_varint_{{varint_kind}}()?{{if conv_type}}.{{conv_type}}(){{end}}"""
    )?

    read_fixed = Template.parse(
      """
      | ({{number}}, {{wire_type}}) =>
              {{name}} = reader.read_fixed_{{fixed_size}}_{{fixed_kind}}()?{{if conv_type}}.{{conv_type}}(){{end}}"""
    )?

    read_packed_varint = Template.parse(
      """
      | ({{number}}, DelimitedField) =>
              reader.read_packed_varint{{if needs_zigzag}}_zigzag{{end}}[{{type}}]({{name}})?
            | ({{number}}, {{wire_type}}) =>
              let v = reader.read_varint_{{varint_kind}}()?{{if conv_type}}.{{conv_type}}(){{end}}
              {{name}}.push(v)"""
    )?

    read_packed_fixed = Template.parse(
      """
      | ({{number}}, DelimitedField) =>
              reader.read_packed_fixed_{{fixed_size}}[{{type}}]({{name}})?
            | ({{number}}, {{wire_type}}) =>
              let v = reader.read_fixed_{{fixed_size}}_{{fixed_kind}}()?{{if conv_type}}.{{conv_type}}(){{end}}
              {{name}}.push(v)"""
    )?

    read_packed_enum = Template.parse(
      """
      | ({{number}}, DelimitedField) =>
              reader.read_packed_enum[{{type}}]({{name}}, {{enum_builder}})?
            | ({{number}}, {{wire_type}}) =>
              match {{enum_builder}}.from_i32(reader.read_varint_32()?.i32())
              | None => None
              | let v: {{type}} => {{name}}.push(v)
              end"""
    )?

    read_inner_message = Template.parse(
      """
      | ({{number}}, DelimitedField) =>
              // TODO: Maybe call parse_from_stream on the field, don't allocate again
              let v: {{type}} = {{type}}
              v.parse_from_stream(reader.pop_embed()?)?
              {{name}} = v"""
    )?

    read_repeated_inner_message = Template.parse(
      """
      | ({{number}}, DelimitedField) =>
              let v: {{type}} = {{type}}
              v.parse_from_stream(reader.pop_embed()?)?
              {{name}}.push(v)"""
    )?

    read_repeated_bytes = Template.parse(
      """
      | ({{number}}, {{wire_type}}) =>
              {{name}}.push(reader.read_bytes()?)"""
    )?

    read_repeated_string = Template.parse(
      """
      | ({{number}}, {{wire_type}}) =>
              {{name}}.push(reader.read_string()?)"""
    )?

    read_repeated_enum = Template.parse(
      """
      | ({{number}}, {{wire_type}}) =>
              match {{enum_builder}}.from_i32(reader.read_varint_32()?.i32())
              | None => None
              | let v: {{type}} => {{name}}.push(v)
              end"""
    )?

    read_repeated_varint = Template.parse(
      """
      | ({{number}}, {{wire_type}}) =>
              {{name}}.push(reader.read_varint_{{varint_kind}}()?{{if conv_type}}.{{conv_type}}(){{end}})"""
    )?

    read_repeated_fixed = Template.parse(
      """
      | ({{number}}, {{wire_type}}) =>
              {{name}}.push(reader.read_fixed_{{fixed_size}}_{{fixed_kind}}()?{{if conv_type}}.{{conv_type}}(){{end}})"""
    )?

    write_optional_clause = Template.parse(
      """
      match {{field}}
          | None => None
          | let {{field}}': {{if needs_viewpoint}}this->{{end}}{{type}} =>
            {{body}}
          end"""
    )?

    write_bytes = Template.parse(
      """
      writer.write_tag({{number}}, DelimitedField)
            writer.write_bytes({{field}})"""
    )?

    write_enum = Template.parse(
      """
      writer.write_tag({{number}}, VarintField)
            writer.write_enum({{field}})"""
    )?

    write_varint = Template.parse(
      """
      writer.write_tag({{number}}, VarintField)
            writer.write_varint[{{type}}]({{field}})"""
    )?

    write_varint_zigzag = Template.parse(
      """
      writer.write_tag({{number}}, VarintField)
            writer.write_varint_zigzag[{{type}}]({{field}})"""
    )?

    write_fixed_32 = Template.parse(
      """
      writer.write_tag({{number}}, Fixed32Field)
            writer.write_fixed_32[{{type}}]({{field}})"""
    )?

    write_fixed_64 = Template.parse(
      """
      writer.write_tag({{number}}, Fixed32Field)
            writer.write_fixed_64[{{type}}]({{field}})"""
    )?

    write_inner_message = Template.parse(
      """
      writer.write_tag({{number}}, DelimitedField)
            // TODO: Don't recompute size here, it's wasteful
            writer.write_varint[U32]({{field}}.compute_size())
            {{field}}.write_to_stream(writer)"""
    )?

    write_repeated_non_message_type = Template.parse(
      """
      for v in {{field}}.values() do
            writer.write_tag({{number}}, {{tag_kind}})
            writer.{{method}}{{if type}}[{{type}}]{{end}}(v)
          end"""
    )?

    write_packed_varint = Template.parse(
      """
      if {{field}}.size() != 0 then
            var {{field}}_size: U32 = 0
            for v in {{field}}.values() do
              {{field}}_size = {{field}}_size + FieldSize.raw_varint(v.u64())
            end
            writer.write_tag({{number}}, DelimitedField)
            writer.write_packed_varint[{{type}}]({{field}}, {{field}}_size)
          end"""
    )?

    // Bad API design on my part :^(
    write_packed_varint_bool = Template.parse(
      """
      if {{field}}.size() != 0 then
            var {{field}}_size: U32 = 0
            for v in {{field}}.values() do
              {{field}}_size = {{field}}_size + FieldSize.raw_varint(if v then 1 else 0 end)
            end
            writer.write_tag({{number}}, DelimitedField)
            writer.write_packed_varint[{{type}}]({{field}}, {{field}}_size)
          end"""
    )?

    write_repeated_inner_message_clause = Template.parse(
      """
      for v in {{field}}.values() do
            writer.write_tag({{number}}, DelimitedField)
            // TODO: Don't recompute size here, it's wasteful
            writer.write_varint[U32](v.compute_size())
            v.write_to_stream(writer)
          end"""
    )?
